plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def globalProperties = new Properties()
def globalPropertiesFile = rootProject.file("global.properties")
if (globalPropertiesFile.exists()) {
    globalPropertiesFile.withReader("UTF-8") { reader ->
        globalProperties.load(reader)
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    // Try getting from environment variable
    flutterVersionCode = System.getenv("ANDROID_VERSION_CODE")
    if (flutterVersionCode == null) {
        flutterVersionCode = "1"
        println "Warning: flutter.versionCode not found in local.properties or ENV, using default: ${flutterVersionCode}"
    } else {
        println "Using flutter.versionCode from ENV: ${flutterVersionCode}"
    }
} else {
    println "Using flutter.versionCode from local.properties: ${flutterVersionCode}"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    // Try getting from environment variable
    flutterVersionName = System.getenv("ANDROID_VERSION_NAME")
    if (flutterVersionName == null) {
        flutterVersionName = "1.0.0"
        println "Warning: flutter.versionName not found in local.properties or ENV, using default: ${flutterVersionName}"
    } else {
        println "Using flutter.versionName from ENV: ${flutterVersionName}"
    }
} else {
    println "Using flutter.versionName from local.properties: ${flutterVersionName}"
}


android {
    namespace = "de.buseslaar.movetopia"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = globalProperties.getProperty('flutter.ndkVersion')

    // BuildConfig-Feature aktivieren
    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    defaultConfig {
        applicationId = "de.buseslaar.movetopia"
        minSdk = globalProperties.getProperty('flutter.minSdkVersion').toInteger()
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName

        // BuildConfig-Felder fÃ¼r die Privacy Policy
        buildConfigField "String", "PRIVACY_POLICY_URL", "\"https://movetopia.de/privacy.html\""
    }

    signingConfigs {
        create("release") {
            storeFile = file(System.getenv("KEYSTORE_PATH") ?: System.getenv("MT_KEYSTORE_PATH"))
            storePassword = System.getenv("KEYSTORE_PASSWORD") ?: System.getenv("MT_KEY_PASSWORD")
            keyAlias = System.getenv("KEY_ALIAS") ?: System.getenv("MT_KEY_PASSWORD")
            keyPassword = System.getenv("KEY_PASSWORD") ?: System.getenv("MT_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")

            // Release-specific settings
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "@string/app_name_debug"
            debuggable true
            versionNameSuffix "-debug"
        }
    }

    // Create a resource file for Fastlane to use
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "movetopia-${variant.buildType.name}.apk"
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.7.0"
}
